/********************************************************************\
*   v0.2 by zd_                                                      *
*   designed for use with GlovePIE 0.41                              *
*                                                                    *
*   This script makes it possible to use a second mouse              *
*   in games that accept Gamepad-Input.                              *
*   This is realized by passing Mouse-Input to the Analog-Stick      *
*   of a virtual gamepad.                                            *
*   Virtual Gamepad: PPJoy: http://ppjoy.blogspot.com/               *
*                                                                    *
*                                                                    *
*    You can use this script in combination with MousePad_l4d2.cfg   *
*    ( http://github.com/zd/MousePad/raw/master/MousePad_l4d2.cfg )  *
*    in Left for Dead (2) to control the second player with a        *
*    second mouse and a second keyboard.                             *
*    Forum Post:                                                     *
*    http://forums.steampowered.com/forums/showthread.php?t=1096997  *
*                                                                    *
*    To check if mouse2 input is passed correctly to the             *
*    PPJoyGamepad, have a look at Window's GameController Settings.  *
*                                                                    *
*    Chose your Button-config by setting var.config below            *
*    You can edit the mapped buttons starting from line 74           *
*                                                                    *
\********************************************************************/


/***** Chose Button Mapping *****\
* var.config = 0 : use mouse2 in combination with a gamepad (for movement) - this is the only way to get things running in GlovePIE prior to v0.41
* var.config = 1 : player2 movement is mapped to numpad of keyboard1
* var.config = 2 : player2 movement is mapped to numpad of keyboard2 - WARNING: player2's keys must not be the same as player1 keys, otherwise they will move P1 as well
                   ... as I wasn't successfull in getting keyboard.swallow to work like mouse.swallow ...
\********************************/
var.config = 2


// Only mouse1 controls the cursor
mouse.Swallow = true
mouse.DirectInputX = mouse.DirectInputX + Delta(mouse1.DirectInputX)
mouse.DirectInputY = mouse.DirectInputY + Delta(mouse1.DirectInputY)
mouse.LeftButton = mouse1.LeftButton
mouse.RightButton = mouse1.RightButton
mouse.MiddleButton = mouse1.MiddleButton
mouse.XButton1 = mouse1.XButton1
mouse.XButton2 = mouse1.XButton2
mouse.Button6 = mouse1.Button6
mouse.Button7 = mouse1.Button7
mouse.Button8 = mouse1.Button8

// is mouse2 present?
if mouse2.present then

  // Mouse2 Buttons are passed to PPJoyButtons
  PPJoy2.Digital0 = mouse2.LeftButton
  PPJoy2.Digital1 = mouse2.RightButton
  PPJoy2.Digital2 = mouse2.MiddleButton

     /* ***
     *   if you use this script to control the second player in Left for Dead (2),
     *   by using MousePad_l4d2.cfg in L4D (which maps L4D-Actions to PPJoy Buttons):
     *
     *   PPJoy2.Analog4 == joy_advaxisu inside MousePad.cfg (set to 1 == (move forward and back))
     *   PPJoy2.Analog5 == joy_advaxisr inside MousePad.cfg (set to 3 == (strafing left and right))
     *** */

  if var.config = 0 then
     /* ***
     *   If you do something like:
     *   "PPJoy2.Digital7 = Keyboard.NUMPADPLUS" in GlovePIE Versions prior to 0.41
     *   the second mouse suddenly moves the first mouse (cursor) again!
     *   => you have to use a real gamepad to control the second player
     *** */

     PPJoy2.Analog4 = Joystick1.y
     PPJoy2.Analog5 = Joystick1.x
     PPJoy2.Digital7 = Joystick1.Button1
     PPJoy2.Digital8 = Joystick1.Button2
     PPJoy2.Digital9 = Joystick1.Button3
     PPJoy2.Digital10 = Joystick1.Button4
     PPJoy2.Digital11 = Joystick1.Button5
     PPJoy2.Digital12 = Joystick1.Button6
     PPJoy2.Digital13 = Joystick1.Button7
     PPJoy2.Digital14 = Joystick1.Button8
     PPJoy2.Digital15 = Joystick1.Button9
  endif

  if var.config = 1 then
     // works with GlovePIE 0.41
     if not PPJoy2.Analog4 = -1 then PPJoy2.Analog4 = Keyboard.NUMPAD5
     if not PPJoy2.Analog4 = 1 then PPJoy2.Analog4 = -Keyboard.NUMPAD8
     if not PPJoy2.Analog5 = -1 then PPJoy2.Analog5 = Keyboard.NUMPAD6
     if not PPJoy2.Analog5 = 1 then PPJoy2.Analog5 = -Keyboard.NUMPAD4
     PPJoy2.Digital2 = Keyboard.NUMPADPLUS
     PPJoy2.Digital7 = Keyboard.Divide
     PPJoy2.Digital8 = Keyboard.Multiply
     PPJoy2.Digital9 = Keyboard.NUMPAD0
     PPJoy2.Digital10 = Keyboard.NUMPADDOT
     PPJoy2.Digital11 = Keyboard.NUMPADENTER
     PPJoy2.Digital12 = Keyboard.End
     PPJoy2.Digital13 = Keyboard.NUMPADMINUS
     PPJoy2.Digital14 = Keyboard.Pageup
     PPJoy2.Digital15 = Keyboard.Pagedown

  endif

  if var.config = 2 then
     // works with GlovePIE 0.41
     if not PPJoy2.Analog4 = -1 then PPJoy2.Analog4 = Keyboard.NUMPAD5
     if not PPJoy2.Analog4 = 1 then PPJoy2.Analog4 = -Keyboard.NUMPAD8
     if not PPJoy2.Analog5 = -1 then PPJoy2.Analog5 = Keyboard.NUMPAD6
     if not PPJoy2.Analog5 = 1 then PPJoy2.Analog5 = -Keyboard.NUMPAD4
     PPJoy2.Digital2 = Keyboard2.NUMPADPLUS
     PPJoy2.Digital7 = Keyboard2.Divide
     PPJoy2.Digital8 = Keyboard2.Multiply
     PPJoy2.Digital9 = Keyboard2.NUMPAD0
     PPJoy2.Digital10 = Keyboard2.NUMPADDOT
     PPJoy2.Digital11 = Keyboard2.NUMPADENTER
     PPJoy2.Digital12 = Keyboard2.End
     PPJoy2.Digital13 = Keyboard2.NUMPADMINUS
     PPJoy2.Digital14 = Keyboard2.Pageup
     PPJoy2.Digital15 = Keyboard2.Pagedown
  endif

  var.deltax2 = Delta(mouse2.DirectInputX)
  var.deltay2 = Delta(mouse2.DirectInputY)

  /* ***
  *  The following part is a 1:1 copy from GlovePIE's MouseParty.PIE Script
  *  Thank you unknown coder! :D
  *** */

  // Mouse ballistics attempt
  var.velocity = ([(smooth(var.deltaX2) / 24ms) in inches per second, (smooth(var.deltaY2) / 24ms) in inches per second] )
  var.speed = |var.velocity|
  if var.speed <= 0.2 then
    var.deltax2 = var.deltax2 * 2
    var.deltay2 = var.deltay2 * 2
  endif
  if var.speed > 0.2 and var.speed <= 0.4 then
    var.deltax2 = var.deltax2 * 4
    var.deltay2 = var.deltay2 * 4
  endif
  if var.speed > 0.4 and var.speed <= 1.25 then
    var.deltax2 = var.deltax2 * 7
    var.deltay2 = var.deltay2 * 7
  endif
  if var.speed > 1.25 and var.speed <= 3.9 then
    var.deltax2 = var.deltax2 *12 //* 7
    var.deltay2 = var.deltay2 *12 //* 7
  endif
  if var.speed > 3.9 then
    var.deltax2 = var.deltax2 * 20// * 13.77
    var.deltay2 = var.deltay2 * 20// * 13.77
  endif
  var.Mouse2x = var.Mouse2x pixels + (var.deltax2 in pixels)
  var.Mouse2y = var.Mouse2y pixels + (var.deltay2 in pixels)
  if var.Mouse2x < screen.DesktopLeft then var.Mouse2x = screen.DesktopLeft
  if var.Mouse2x > screen.DesktopRight then var.Mouse2x = screen.DesktopRight
  if var.Mouse2y < screen.DesktopTop then var.Mouse2y = screen.DesktopTop
  if var.Mouse2y > screen.DesktopBottom then var.Mouse2y = screen.DesktopBottom

  /* ***
  * When using MousePad_l4d2.cfg in L4D:
  * PPJoy2.Analog0 == joy_advaxisx inside MousePad.cfg (set to 4 == (look left and right))
  * PPJoy2.Analog1 == joy_advaxisy inside MousePad.cfg (set to 2 == (look up and down))
  *** */
  PPJoy2.Analog0 = MapRange(var.deltax2, (-1279),1279, (-1),1)
  PPJoy2.Analog1 = MapRange(var.deltay2, (-1023),1023, (-1),1)

  // shows mouse movement debug information
  debug = 'PPJoy Analog Input (from mouse2) - dx: ' + PPJoy2.Analog0 + ' | dy: ' + PPJoy2.Analog1

endif

